#include "rwmake.ch"

User Function RFINA135()

/*/


Ŀ
Programa   RFINA134  Autor         Nava            Data  15/01/08 
Ĵ
Descrio  Envio de Contribuicoes Asociativa E Confederativa  (ITAU)  
Ĵ
Uso        Especifico SINDHOSP - Carteira 112                         
Ĵ
Obs.:                                                                 
ٱ


/*/
LOCAL aArea 	:= GetArea()
LOCAL cTitulo  := "Envio da Cobranca Associativa para o Itau"
LOCAL nOpc     := 0
LOCAL cPerg    := "FIN134"
LOCAL oDlg
LOCAL cNumBord := Strzero( Val( GETMV("MV_NUMBORR") ) + 1, 6 )
/*
Ŀ
 Variaveis utilizadas para parametros                                       
 mv_par01   // Mes para envio                                               
 mv_par02   // ano para envio                                               
 mv_par03   // Data de vencimento                                           
 mv_par04   // Sequencia       															  
 mv_par05   // arquivo de saida := "H:\BCOREAL\ASSOC"+strzero(MES,2)+".txt" 
 mv_par06   // Sindicato de Hospitais
 mv_par07   // Natureza              
 mv_par08   // codigo do Banco                                              
 mv_par09   // codigo da agencia                                            
 mv_par10   // codigo da conta                                              
 mv_par11   // codigo da subconta                                           
 mv_par12   // Mensagem Linha 01                                            
 mv_par13   // Mensagem Linha 02                                            
 mv_par14   // Mensagem Linha 03                                            
 mv_par15   // Mensagem Linha 04                                            
 mv_par16   // Mensagem Linha 05                                            
 mv_par17   // Hist Assoc Preenchido ?                                      

*/
Pergunte(cPerg,.T.)

@ 096,042 TO 375,505 DIALOG oDlg TITLE cTitulo
@ 008,010 TO 100,222
@ 023,014 SAY "Este programa fara a geracao de Arquivo para cobranca da "
@ 033,014 SAY "Contribuicao Associativa E Confederativa no ITAU"
@ 043,014 SAY "BORDERO : " + cNumBord
@ 063,014 SAY "CONFIRME PARA INICIAR A GERACAO DO ARQUIVO "

@ 110,138 BMPBUTTON TYPE 1 ACTION Iniciar( oDlg, cNumBord )
@ 110,168 BMPBUTTON TYPE 5 ACTION Pergunte(cPerg,.T.)
@ 110,198 BMPBUTTON TYPE 2 ACTION Close(oDlg)

ACTIVATE DIALOG oDlg CENTERED

RestArea( aArea )

Return


/*===============================*/
  Static Function Iniciar( oDlg, cNumBord )
/*===============================*/

Close(oDlg)

Processa( {|| Gerar(cNumBord) })

Return

/*=========================*/
   Static Function Gerar(cNumBord)
/*=========================*/

LOCAL aStru := {}
LOCAL cVencim 		:= strzero(day(mv_par03),2)+strzero(month(mv_par03),2)+strzero(val(substr(str(year(mv_par03),4),3,2)),2)
LOCAL cEmissao 	:= strzero(day(dDataBase),2)+strzero(month(dDataBase),2)+strzero(val(substr(str(year(dDataBase),4),3,2)),2)
LOCAL nRegistros 	:= 0
LOCAL nIncluidos 	:= 0
LOCAL cTitulo 		:= "Gerando Arquivo Bancario - Associativa - ITAU"
LOCAL nSeq 			:= "000002"
LOCAL nValTot 		:= 0
LOCAL cNumborr 	:= val( getMV("MV_NUMBORR")) +1
LOCAL cPeriodo 	:= substr( cVencim, 5, 2 ) + substr( cVencim, 3, 2 )

PRIVATE cArqTmp1
PRIVATE cArqTmp2
PRIVATE cArqTmp3
PRIVATE cArqTmp4

/*BEGINDOC
//Ŀ
//ARQUIVO REMESSA	REGISTRO HEADER	TAMANHO DO REGISTRO = 400 Bytes                          		
//                                                                                        				
//NOME DO CAMPO			SIGNIFICADO	POSIO   	PICTURE	CONTEDO                               		
//Ŀ
//TIPO DE  REGISTRO		IDENTIFICAO DO REGISTRO HEADER			 	001   001	9(01)		0        		
//OPERAO 				TIPO DE OPERAO - REMESSA						002   002	9(01) 	1        		
//LITERAL DE REMESSA	IDENTIFICAO POR EXTENSO DO MOVIMENTO 	003   009	X(07) 	REMESSA  		
//CDIGO DO SERVIO 	IDENTIFICAO DO TIPO DE SERVIO				010   011	9(02) 	01       		
//LITERAL DE SERVIO 	IDENTIFICAO POR EXTENSO DO SERVIO		012   026	X(15) 	COBRANCA 		
//AGNCIA					AGNCIA MANTENEDORA DA CONTA					027   030	9(04)	            		
//ZEROS						COMPLEMENTO DE REGISTRO							031   032	9(02)		"00"     		
//CONTA						NMERO DA CONTA CORRENTE DA EMPRESA			033   037	9(05)	            		
//DAC						DGITO DE AUTO CONFERNCIA AG/CONTA 		038   038	9(01)	            		
//BRANCOS 					COMPLEMENTO DO REGISTRO 						039   046	X(08)  	         		
//NOME DA EMPRESA 		NOME POR EXTENSO DA "EMPRESA ME" 			047   076	X(30)  	         		
//CDIGO DO BANCO 		N DO BANCO NA CMARA DE COMPENSAO		077   079	9(03)  	341      		
//NOME DO BANCO			NOME POR EXTENSO DO BANCO COBRADOR 			080   094	X(15) 	BANCO ITAU SA  
//DATA DE GERAO		DATA DE GERAO DO ARQUIVO 					095   100	9(06)		DDMMAA         
//BRANCOS 					COMPLEMENTO DO REGISTRO							101   394	X(294)	               
//NMERO SEQENCIAL 	NMERO SEQENCIAL DO REGISTRO NO ARQUIVO 	395   400	9(06) 	000001         
//
ENDDOC*/
aStru := {}  //REGISTRO HEADER
AADD( aStru, { "COD_REGIST"  , "C" , 001 , 0 } )
AADD( aStru, { "CONSTANTE"   , "C" , 025 , 0 } )
AADD( aStru, { "AGE_CEDENT"  , "C" , 004 , 0 } )
AADD( aStru, { "ZERO"        , "C" , 002 , 0 } )
AADD( aStru, { "CTA_CEDENT"  , "C" , 005 , 0 } )
AADD( aStru, { "DAC"         , "C" , 001 , 0 } )
AADD( aStru, { "BRANCO1"     , "C" , 008 , 0 } )
AADD( aStru, { "NOME_CEDEN"  , "C" , 030 , 0 } )
AADD( aStru, { "IDENT_BANC"  , "C" , 003 , 0 } )
AADD( aStru, { "NOME_BANCO"  , "C" , 015 , 0 } )
AADD( aStru, { "DATA_PROCE"  , "C" , 006 , 0 } )
AADD( aStru, { "BRANCO2"     , "C" , 294 , 0 } )
AADD( aStru, { "NUM_SEQUEN"  , "C" , 006 , 0 } )

cArqTmp1 := CriaTrab( aStru,.t.)
DBUSEAREA(.T.,, cArqTmp1, "HEAD")
dbSelectArea("HEAD")

aStru := {}  //REGISTRO TRAILLER
AADD( aStru, { "COD_REGIS"  , "C" , 001 , 0 } )
AADD( aStru, { "BRANCO"     , "C" , 393 , 0 } )
AADD( aStru, { "NUM_SEQUEN" , "C" , 006 , 0 } )

cArqTmp2 := CriaTrab( aStru,.t.)
dbUseArea(.t.,, cArqTmp2, "TRAI")

dbSelectArea("TRAI")
aStru := {} // REGISTRO DETALHE

/*BEGINDOC
//Ŀ
//ARQUIVO REMESSA		REGISTRO DETALHER	TAMANHO DO REGISTRO = 400 Bytes                        		
//                                                                                        				
//NOME DO CAMPO			SIGNIFICADO	POSIO   	PICTURE	CONTEDO                               		
//Ŀ
//TIPO DE  REGISTRO		IDENTIFICAO DO REGISTRO HEADER			 	001   001	9(01)		0        		
//CDIGO DE LAYOUT 		IDENTIFICAO DO LAYOUT PARA O REGISTRO 	002   002 	9(01)		1        		
//AGNCIA					AGNCIA MANTENEDORA DA CONTA					003   006	9(04)	        				
//ZEROS						COMPLEMENTO DE REGISTRO							007   008	9(02)		"00"        	
//CONTA						NMERO DA CONTA CORRENTE DA EMPRESA			009   013	9(05)	   					
//DAC						DGITO DE AUTO CONFERNCIA AG/CONTA 		014   014	9(01)	        				
//N DA CARTEIRA			N DA CARTEIRA NO BANCO							015   017	9(03)	        				
//NOSSO NMERO			IDENTIFICAO DO TTULO NO BANCO				018   025	9(08)	        				
//DAC						DAC DO NOSSO NMERO								026   026	9(01)		NOTA 23        
//CDIGO DA MOEDA 		INDICA SE O VALOR EM REAL OU EM MOEDA 		027   027	9(01)		0 = R$ 1 = VAR 
//LITERAL DE MOEDA  	IDENTIF. DA MOEDA A SER IMPRESSA 			028   031	X(04)		NOTA 24        
//VALOR DO TTULO		VALOR DO TTULO									032   044 	9(11)(2)	(*)        		
//SEU NMERO				NMERO DO DOCUMENTO NA EMPRESA 				045   054 	X(10)		NOTA 18        
//VENCIMENTO 				DATA DE VENCIMENTO DO TTULO					055   060 	9(06)		DDMMAA        	
//ESPCIE 					ESPCIE DO TTULO 								061   062	X(02)		NOTA 10        
//ACEITE  					IDENTIFICAO DE TTULO ACEITO OU NO 		063   063	X(01)		A=SIM N=NO   
//DATA DE EMISSO 		DATA DE EMISSO DO TTULO						064   069	9(06)		DDMMAA        	
//CD. DE INSCRIO		IDENTIFICAO DE INSCRIO DO SACADO		070   071	9(02)		01=CPF 02=CNPJ 
//N DE INSCRIO		NMERO DE INSCRIO DO SACADO 				072   086	9(15)		CPF OU CNPJ    
//NOME 						NOME DO SACADO										087   116	X(30)	        				
//BRANCOS 					COMPLEMENTO DO REGISTRO							117   125	X(09)	        				
//LOGRADOURO 				RUA, NMERO E COMPLEMENTO DO SACADO			126   165 	X(40)	        				
//BAIRRO 					BAIRRO DO SACADO 									166   177	X(12)	        				
//CEP 						CDIGO DE ENDEREAMENTO POSTAL DO SACADO	178   185	9(08)	        				
//CIDADE					CIDADE DO SACADO									186   200	X(15)	        				
//ESTADO					ESTADO DO SACADO									201   202	X(02)	        				
//SACADOR/AVALISTA		NOME DO SACADOR/AVALISTA 						203   232	X(30)	        				
//BRANCOS					COMPLEMENTO DE REGISTRO							233   236	X(04)	        				
//LOCAL DE PGTO 1		LOCAL PARA PAGAMENTO DO TTULO - LINHA 1	237   291	X(55)		NOTA 25        
//LOCAL DE PGTO 2 		LOCAL PARA PAGAMENTO DO TTULO - LINHA 2 	292   346	X(55)		NOTA 25        
//CD. DE INSCRIO.	TIPO DE INSCRIO DO SACADOR/AVALISTA		347   348 	9(02)		01=CPF 02=CNPJ 
//N DE INSCRIO		NMERO DE INSCRIO DO SACADOR/AVALISTA	349   363	9(15)	        				
//BRANCOS 					COMPLEMENTO DO REGISTRO							364   394	X(31)	        				
//NM. SEQENCIAL 		NMERO SEQENCIAL DO REGISTRO NO ARQUIVO 	395   400	9(06)	        				
//
ENDDOC*/
AADD( aStru, { "COD_REGIST"  , "C" , 001 , 0 } ) // 6
AADD( aStru, { "COD_LAYOUT"  , "C" , 001 , 0 } ) // 1
AADD( aStru, { "AG_CEDENTE"  , "C" , 004 , 0 } )
AADD( aStru, { "ZERO_1"      , "C" , 002 , 0 } ) // 00
AADD( aStru, { "CT_CEDENTE"  , "C" , 005 , 0 } )
AADD( aStru, { "DAC_AGCONT"  , "C" , 001 , 0 } )
AADD( aStru, { "NUM_CARTEI"  , "C" , 003 , 0 } ) // 173
AADD( aStru, { "NOSSO_NUM"   , "C" , 008 , 0 } ) // 00000000
AADD( aStru, { "DAC_NOSSON"  , "C" , 001 , 0 } )
AADD( aStru, { "COD_MOEDA"   , "C" , 001 , 0 } ) // 0
AADD( aStru, { "LIT_MOEDA"   , "C" , 004 , 0 } ) // 
AADD( aStru, { "VLR_TITULO"  , "C" , 013 , 0 } )
AADD( aStru, { "SEU_NUM"     , "C" , 010 , 0 } ) // 00000000
AADD( aStru, { "DATA_VENC"   , "C" , 006 , 0 } )
AADD( aStru, { "ESPECIE"     , "C" , 002 , 0 } ) // 01 
AADD( aStru, { "ACEITE"      , "C" , 001 , 0 } ) // "N"
AADD( aStru, { "DATA_EMISS"  , "C" , 006 , 0 } )
AADD( aStru, { "COD_INSCRI"  , "C" , 002 , 0 } ) // 02
AADD( aStru, { "CGC_SACADO"  , "C" , 015 , 0 } )
AADD( aStru, { "NOME_SACAD"  , "C" , 030 , 0 } )
AADD( aStru, { "BRANCO2"     , "C" , 009 , 0 } )
AADD( aStru, { "END_SACADO"  , "C" , 040 , 0 } )
AADD( aStru, { "BAIRRO_SAC"  , "C" , 012 , 0 } )
AADD( aStru, { "CEP_SACADO"  , "C" , 008 , 0 } )
AADD( aStru, { "CIDADE_SAC"  , "C" , 015 , 0 } )
AADD( aStru, { "ESTADO_SAC"  , "C" , 002 , 0 } )
AADD( aStru, { "SACADOR"     , "C" , 030 , 0 } )
AADD( aStru, { "BRANCO1"     , "C" , 004 , 0 } )
AADD( aStru, { "LOCAL_1"     , "C" , 055 , 0 } )
AADD( aStru, { "LOCAL_2"     , "C" , 055 , 0 } )
AADD( aStru, { "CPF_OU_CGC"  , "C" , 002 , 0 } ) // 02
AADD( aStru, { "CGC_SINDHO"  , "C" , 015 , 0 } )
AADD( aStru, { "BRANCO3"     , "C" , 031 , 0 } )
AADD( aStru, { "NUM_SEQUEN"  , "C" , 006 , 0 } )

cArqTmp3 := CriaTrab( aStru, .T.)
dbUseArea(.t.,, cArqTmp3,"DETA")
dbSelectArea("DETA")

aStru := {}  //ARQUIVO FINAL
AADD( aStru, { "REGISTRO"  , "C" , 400 , 0 } )
cArqTmp4 := CriaTrab( aStru,.t.)
dbUseArea(.t.,,cArqTmp4,"REGI")
dbSelectArea("REGI")

/*BEGINDOC
//Ŀ
//ARQUIVO REMESSA		REGISTRO DETALHER	TAMANHO DO REGISTRO = 400 Bytes                        		
//                                                                                        				
//NOME DO CAMPO			SIGNIFICADO	POSIO   	PICTURE	CONTEDO                               		
//Ŀ
// TIPO DE  REGISTRO		IDENTIFICAO DO REGISTRO HEADER			 	001   001	9(01)		6        	
// CDIGO DE LAYOUT 		IDENTIFICAO DO LAYOUT PARA O REGISTRO 	002   002 	9(01)		2        	
// LINHA 1						CONTEDO DA 1 LINHA 							003   071	X(69)	NOTA 26     	
// LINHA 2 					CONTEDO DA 2 LINHA 							072   140	X(69)	NOTA 26     	
// LINHA 3						CONTEDO DA 3 LINHA 							141   209	X(69)	NOTA 26     	
// LINHA 4 					CONTEDO DA 4 LINHA 							210   278	X(69)	NOTA 26     	
// LINHA 5						CONTEDO DA 5 LINHA 							279   347	X(69)	NOTA 26     	
// BRANCOS 					COMPLEMENTO DO REGISTRO							364   394	X(47)	        			
// NM. SEQENCIAL 		NMERO SEQENCIAL DO REGISTRO NO ARQUIVO 		395   400	9(06)	        			
//

aStru := {}
AADD( aStru, { "COD_REGIST"  	, "C" , 001 , 0 } ) // 6
AADD( aStru, { "COD_LAYOUT" 	, "C" , 001 , 0 } ) // 2
AADD( aStru, { "LINHA_1" 	   , "C" , 069 , 0 } )
AADD( aStru, { "LINHA_2" 		, "C" , 069 , 0 } )
AADD( aStru, { "LINHA_3"	 	, "C" , 069 , 0 } )
AADD( aStru, { "LINHA_4"	 	, "C" , 069 , 0 } )
AADD( aStru, { "LINHA_5"	 	, "C" , 069 , 0 } )
AADD( aStru, { "BRANCO"    	, "C" , 047 , 0 } )
AADD( aStru, { "NUM_SEQUEN" 	, "C" , 006 , 0 } )
ENDDOC*/

aStru := {}
AADD( aStru, { "CODIGO" 		, "C" , 001 , 0 } )
AADD( aStru, { "FLASH"  		, "C" , 003 , 0 } )
AADD( aStru, { "LINHA_1" 	   , "C" , 002 , 0 } )
AADD( aStru, { "MENSAGEM_1" 	, "C" , 100 , 0 } )
AADD( aStru, { "BRANCO_1"   	, "C" , 028 , 0 } )
AADD( aStru, { "LINHA_2" 		, "C" , 002 , 0 } )
AADD( aStru, { "MENSAGEM_2" 	, "C" , 100 , 0 } )
AADD( aStru, { "BRANCO_2"   	, "C" , 028 , 0 } )
AADD( aStru, { "LINHA_3"	 	, "C" , 002 , 0 } )
AADD( aStru, { "MENSAGEM_3" 	, "C" , 100 , 0 } )
AADD( aStru, { "BRANCO_3"    	, "C" , 028 , 0 } )
AADD( aStru, { "NUM_SEQUEN" 	, "C" , 006 , 0 } )

cArqTmp5 := CriaTrab( aStru,.t.)
dbUseArea(.t.,,cArqTmp5,"MSG")

dbSelectArea("SA1")
dbsetorder(3)
dbGotop()
ProcRegua(RecCount(),18,05)

Do While !Eof()
	IncProc("Cliente: "+sa1->a1_cod+"-"+sa1->a1_nreduz)
	IF LEN(ALLTRIM(SA1->A1_CEP))<> 8
		DBSELECTAREA("SA1")
		DBSKIP()
		LOOP
	ENDIF

	dbSelectArea("SA1")

	// Rodrigo - Desabilitado porque todas as naturezas jah sao 1.000
/*
	if sa1->a1_catleit $"66/67/68/70/71/72"
		reclock("sa1",.f.)
		sa1->a1_naturez:="1000"
		msunlock()
	endif
*/
	If ! EMPTY( MV_PAR06) .AND. SA1->A1_SINDICA <> MV_PAR06
		dbSelectArea("SA1")
		dbskip()
		loop
	EndIf

	If !EMPTY(SA1->A1_INAT)
		dbSelectArea("SA1")
		dbskip()
		loop
	EndIf

	if alltrim(SA1->A1_NATUREZ)<>Rtrim( mv_par07 )   && Associativa + Confederativa
		dbSelectArea("SA1")
		dbskip()
		Loop
	EndIf

	IF !EMPTY(MV_PAR17) .AND. SA1->A1_INISIND < MV_PAR17
		dbskip()
		Loop
	EndIf
	
	IF !EMPTY(MV_PAR18) .AND. SA1->A1_INISIND > MV_PAR18
		dbskip()
		Loop
	EndIf

	If empty(SA1->A1_CODASSO)
		dbskip()
		Loop
	Endif
	if SA1->A1_SITUAC <> "AS" .and. SA1->A1_SITUAC <> "SS"
		dbskip()
		Loop
	Endif
	If !empty(SA1->A1_DTFIMAS)
		dbskip()
		Loop
	Endif
	if val(SA1->A1_CATLEIT) <= 0
		dbskip()
		Loop
	Endif
	if val( TABELA("90",SA1->A1_CATLEIT) ) <= 0
		dbskip()
		Loop
	Endif

	dbSelectArea("SE1")
	dbsetorder(1)
	IF dbseek(xFilial("SE1")+strzero(MV_PAR02,2)+" "+strzero(MV_PAR01,2)+substr(SA1->A1_CODASSO,3,4))
		dbSelectArea("SA1")
		dbskip()
		Loop
	Endif
	dbSelectArea("DETA")  //GERANDO REGISTRO DETALHE
	RecLock("DETA",.T.)

	DETA->COD_REGIST := "6"
	DETA->COD_LAYOUT := "1"
	DETA->AG_CEDENTE := Rtrim( mv_par09 )
	DETA->ZERO_1     := "00"
	DETA->CT_CEDENTE := Rtrim( mv_par10 )
	DETA->DAC_AGCONT := STR( MODULO10( AllTrim( mv_par09 ) + AllTrim( mv_par10 ) ), 1 )
	DETA->NUM_CARTEI := "173"
	DETA->NOSSO_NUM  := strzero(MV_PAR02,2)+strzero(MV_PAR01,2)+substr(SA1->A1_CODASSO,3,4)
	DETA->DAC_NOSSON := STR( MODULO10( AllTrim( mv_par09 ) + AllTrim( mv_par10 ) + DETA->NUM_CARTEI + DETA->NOSSO_NUM  ), 1 )
	DETA->COD_MOEDA := "0"
	DETA->VLR_TITULO := strzero(val( TABELA("90",SA1->A1_CATLEIT) )*100,13)
	DETA->SEU_NUM    := strzero(MV_PAR02,2)+"0"+strzero(MV_PAR01,2)+substr(SA1->A1_CODASSO,3,4)+"0000"
	DETA->DATA_VENC  := cVencim
	DETA->ESPECIE    := "01"
	DETA->ACEITE     := "N"
	DETA->DATA_EMISS := cEmissao
	DETA->COD_INSCRI := "02"
	DETA->CGC_SACADO := SA1->A1_CGC
	DETA->NOME_SACAD := NoAccent( SA1->A1_NOME   )
	DETA->END_SACADO := NoAccent( SA1->A1_END    )
	DETA->BAIRRO_SAC := NoAccent( SA1->A1_BAIRRO )
	DETA->CEP_SACADO := NoAccent( SA1->A1_CEP    )
	DETA->CIDADE_SAC := NoAccent( SA1->A1_MUN    )
	DETA->ESTADO_SAC := NoAccent( SA1->A1_EST    )
	DETA->SACADOR    := "SINDICATO DOS HOSPITAIS DO EST.SAO PAULO"
	DETA->LOCAL_1    := "ATE O VCTO.,  PAGUE PREFERENCIALMENTE NO ITAU"
	DETA->LOCAL_2    := "APOS O VENCIMENTO, PAGUE SOMENTE NO ITAU"
	DETA->CPF_OU_CGC := "02"
	DETA->CGC_SINDHO := "47436373000173"
	DETA->NUM_SEQUEN := nSeq     //Sequencia

	MsUnlock()

	nSeq					:= strzero( ( VAL( nSeq ) + 1 ), 6 )

	dbSelectArea("MSG")  //GERANDO REGISTRO MENSAGEM
	RecLock("MSG",.T.)

/*
	MSG->COD_REGIST := "6"
	MSG->COD_LAYOUT := "2"
	MSG->LINHA_1	 := NoAccent( MV_PAR12 )
	MSG->LINHA_2	 := NoAccent( MV_PAR13 )
	MSG->LINHA_3	 := NoAccent( MV_PAR14 )
	MSG->LINHA_4	 := NoAccent( MV_PAR15 )
	MSG->LINHA_5	 := NoAccent( MV_PAR16 )
	MSG->NUM_SEQUEN := nSeq     //Sequencia
	MsUnlock()
*/

	MSG->CODIGO 	 := "7"
	MSG->FLASH		 := "L6L"
	MSG->LINHA_1	 := "01"
	MSG->MENSAGEM_1 := SPACE( 7 ) + PADR( NoAccent( SA1->A1_NOME   ), 60 ) + ;
						     PADR( strzero(MV_PAR02,2)+"0"+strzero(MV_PAR01,2)+substr(SA1->A1_CODASSO,3,4)+"0", 17 ) + ;
						     cVencim
	MSG->LINHA_2	 := "04"
	MSG->MENSAGEM_2 := NoAccent( MV_PAR12 )
	MSG->LINHA_3	 := "06"
	MSG->MENSAGEM_3 := NoAccent( MV_PAR13 )

	MSG->NUM_SEQUEN := nSeq     //Sequencia
	MsUnlock()

	nSeq					:= strzero( ( VAL( nSeq ) + 1 ), 6 )

	RecLock("MSG",.T.)
	MSG->CODIGO 	 := "7"
	MSG->FLASH		 := "L6L"
	MSG->LINHA_1	 := "08"
	MSG->MENSAGEM_1 := NoAccent( MV_PAR14 )
	MSG->LINHA_2	 := "10"
	MSG->MENSAGEM_2 := NoAccent( MV_PAR15 )
	MSG->LINHA_3	 := "12"
	MSG->MENSAGEM_3 := NoAccent( MV_PAR16 )
	MSG->NUM_SEQUEN := nSeq     //Sequencia
	MsUnlock()

	nValTot 				+= val( TABELA("90",SA1->A1_CATLEIT) )
	nRegistros 			+= 1
	nSeq					:= strzero( ( VAL( nSeq ) + 1 ), 6 )

	dbSelectArea("SE1")  //GERANDO Contas a receber
	IF ! dbseek(xFilial()+strzero(MV_PAR02,2)+" "+strzero(MV_PAR01,2)+substr(SA1->A1_CODASSO,3,4))
		RecLock("SE1",.T.)         

		SE1->E1_FILIAL   := xFilial("SE1")
		SE1->E1_PREFIXO  := strzero(MV_PAR02,2)+" "
		SE1->E1_NUM      := strzero(MV_PAR01,2)+substr(SA1->A1_CODASSO,3,4)
		SE1->E1_PARCELA  := ""
		SE1->E1_TIPO     := "DP"
		SE1->E1_NATUREZ  := RTRIM( mv_par07 ) //VERIFICAR
		SE1->E1_CLIENTE  := SA1->A1_COD
		SE1->E1_LOJA     := SA1->A1_LOJA
		SE1->E1_NOMCLI   := SA1->A1_NREDUZ
		SE1->E1_EMISSAO  := dDataBase
		SE1->E1_VENCTO   := mv_par03
		SE1->E1_VENCREA  := DataValida( mv_par03,.t.)
		SE1->E1_VALOR    := val( TABELA("90",SA1->A1_CATLEIT) )
		SE1->E1_EMIS1    := dDataBase
		SE1->E1_SITUACA  := "0"
		SE1->E1_SALDO    := val( TABELA("90",SA1->A1_CATLEIT) )
		SE1->E1_VLCRUZ   := val( TABELA("90",SA1->A1_CATLEIT) )
		SE1->E1_VALJUR   := 1
		SE1->E1_VENCORI  := mv_par03
		SE1->E1_MOEDA    := 1
		SE1->E1_HIST     := "Ref.Contr.Assoc. de "+strzero(mv_par01,2)+"/"+strzero(mv_par02,2)
		//APOS BORDERO
		SE1->E1_PORTADO := Rtrim( mv_par08 )//"356"
		SE1->E1_AGEDEP  := Rtrim( mv_par09 )//"1874"
		SE1->E1_CONTA   := Rtrim( mv_par10 )//"5005542"
		SE1->E1_NUMBOR  := strzero(cNumborr,6)  //NUMERO DO BORDERO
		SE1->E1_DATABOR := DDatabase //DATA DO BORDERO
		SE1->E1_MOVIMEN := DDatabase
		SE1->E1_SITUACA := "1"
		SE1->E1_OCORREN := "01"
		SE1->E1_ORIGEM  := "RFINA135"
		SE1->E1_STATUS  := "A"
		SE1->E1_OK      := CHR(56)+CHR (121)
		SE1->E1_CGC     := SA1->A1_CGC
		SE1->E1_CODASSO := SA1->A1_CODASSO
		SE1->E1_CATEG1  := SA1->A1_CATEG1
		SE1->E1_ERSIN   := SA1->A1_ERSIN
		SE1->E1_BASE    := SA1->A1_BASE
		SE1->E1_SINDICA := SA1->A1_SINDICA

		//APOS GERACAO
		//    SE1->E1_NUMBCO := //NOSSO NUMERO
		nIncluidos 		 += 1
		MsUnlock()

		//ATUALIZA-SE O SEA
		dbSelectArea("SEA")  //GERANDO Contas a receber
		RecLock("SEA",.T.)
		SEA->EA_FILIAL  := xFilial("SEA")
		SEA->EA_PREFIXO := strzero(MV_PAR02,2)+" "
		SEA->EA_PARCELA := ""
		SEA->EA_NUM     := strzero(MV_PAR01,2)+substr(SA1->A1_CODASSO,3,4)
		SEA->EA_PORTADO := mv_par08 //"356" - Nava
		SEA->EA_AGEDEP  := mv_par09 // "1874" - Nava
		SEA->EA_NUMCON  := mv_par10 // "7710534" - Nava
		SEA->EA_NUMBOR  := strzero( cNumborr, 6)
		SEA->EA_DATABOR :=  dDatabase
		SEA->EA_TIPO    := "DP"
		SEA->EA_CART    := "R"
		SEA->EA_SITUACA := "1"
		//APOS GERAR ARQUIVO
		SEA->EA_TRANSF := "S"
		MsUnlock()

		RecLock("SA1",.F.)
		SA1->A1_MCOMPRA := SA1->A1_MCOMPRA +val( TABELA("90",SA1->A1_CATLEIT) )
		SA1->A1_SALDUP  := SA1->A1_SALDUP +val( TABELA("90",SA1->A1_CATLEIT) )
		SA1->A1_PRICOM  := if(empty(SA1->A1_PRICOM),dDataBase,SA1->A1_PRICOM)
		SA1->A1_ULTCOM  := dDataBase
		dbcommit()
		MsUnlock()
	ENDIF
	dbSelectArea("SA1")
	dbskip()
EndDO

/* Contabilizar CTB
dbselectArea("SI2")
dbsetorder(3)
dbseek( xFilial() + dtos(dDataBase) + "7500" )
if _nvalTot > 0 .and. eof()
	recLock("SI2",.T.)
	SI2->I2_FILIAL := "01"
	SI2->I2_NUM    := "7500001"
	SI2->I2_LINHA  := "01"
	SI2->I2_DATA   := mv_par03
	SI2->I2_DC     := "X"
	SI2->I2_MOEDAS := "SNNNN"
	SI2->I2_VALOR  := _nvalTot
	SI2->I2_DTVENC := mv_par03
	SI2->I2_ROTINA := "RFINA04"
	SI2->I2_PERIODO:= _cPeriodo
	SI2->I2_ORIGEM := "L.GER P/RFINA04"
	SI2->I2_FILORIG:= "01"
	SI2->I2_DEBITO := "1123010001"
	SI2->I2_CREDITO:= "3111040001"
	SI2->I2_HIST   := "PROVISAO REFERENTE AO MES " + strzero(mv_par01) + "/" + strzero(mv_par02)
	MsUnlock()
endif
*/

DbSelectArea("SX6")
dbseek(xfilial()+"MV_NUMBORR")
if !eof()
	reclock("SX6",.F.)
	SX6->X6_CONTEUD :=  strzero( cNumborr, 6)
	MSunlock()
endif

//ATUALIZA-SE O SEE
dbSelectArea("SEE")  //GERANDO Contas a receber
if dbseek( xFilial("SEE")+MV_PAR08+MV_PAR09+MV_PAR10+MV_PAR11 )
	RecLock("SEE",.F.)
	SEE->EE_ULTDSK := strzero(val(SEE->EE_ULTDSK)+1,6)
	MsUnlock()
Endif

IF VAL( nSeq ) > 2

	dbSelectArea("HEAD")  //GERANDO REGISTRO HEADER
	RecLock("HEAD",.T.)
	
	HEAD->COD_REGIST  := "0"
	HEAD->CONSTANTE   := "1REMESSA01COBRANCA"
	HEAD->AGE_CEDENT  := Rtrim( mv_par09 )
	HEAD->ZERO        := "00"
	HEAD->CTA_CEDENT  := Rtrim( mv_par10 )
	HEAD->DAC         := STR( MODULO10( AllTrim( mv_par09 ) + AllTrim( mv_par10 ) ), 1 )
	HEAD->NOME_CEDEN  := "SINDHOSP-SIND.HOSPITAIS EST SP"
	HEAD->IDENT_BANC  := Rtrim( mv_par08 )
	HEAD->NOME_BANCO  := "BANCO ITAU SA"
	HEAD->DATA_PROCE  := cEmissao
	HEAD->NUM_SEQUEN  := "000001"
	
	MsUnLock()
	COPY TO HEAD.txt SDF

	dbSelectArea("TRAI")  //GERANDO REGISTRO TRAILLER
	RecLock("TRAI",.T.)
	TRAI->COD_REGIS := "9"
	TRAI->NUM_SEQUEN := strzero(val( nSeq), 6 ) //INCREMENTA()
	MsUnlock()
	COPY TO TRAI.TXT SDF

	dbSelectArea("REGI")	    // Gera Trailler
	Append From HEAD.TXT SDF

	dbSelectArea("DETA")
	DbGotop()
	dbSelectArea("MSG")
	DbGotop()

	Do While !(DETA->(eof()))
		dbSelectArea("DETA")
		COPY NEXT 1 TO DETA.TXT SDF
		dbSelectArea("MSG")
		COPY NEXT 2 TO MSG.TXT SDF
		dbSelectArea("REGI")	  // Gera Detalhes e Mensagem
		Append From DETA.TXT SDF
		Append From MSG.TXT SDF
	EndDo
	
	Append From TRAI.TXT SDF
	ARQ := "\arquivos\associativa\"+AllTrim( MV_PAR05 )
	COPY TO &arq SDF	    // Copia arq. definitivo
	
ENDIF

@ 96,42 TO 290,405 DIALOG oDlg TITLE "Termino da Geracao"
@ 8,10 TO 73,170
@ 23,22 SAY "Registros Gerados"
@ 23,95 GET nRegistros PICTURE "@E 999,999" WHEN .F.
@ 38,22 SAY "Registros Incluidos"
@ 38,95 GET nIncluidos PICTURE "@E 999,999" WHEN .F.
@ 53,22 SAY "Numero do Bordero "
@ 53,95 GET cNumBord  PICTURE "@E 999999" WHEN .F.
@ 77,078 BMPBUTTON TYPE 1 ACTION Close(oDlg)
ACTIVATE DIALOG oDlg CENTERED

HEAD->( DbCloseArea() )
MSG->( DbCloseArea() )
TRAI->( DbCloseArea() )
DETA->( DbCloseArea() )
REGI->( DbCloseArea() )

FOR nI := 1 TO 5

   cArqTmp := "cArqTmp" + Str( nI, 1 )
	IF FILE( &cArqTmp  + ".DBF")
	 	Ferase( &cArqTmp  + ".DBF")
	EndIf

Next nI

If File("HEAD.TXT")
  	Ferase("HEAD.TXT")
EndIf
If File("DETA.TXT")
  	Ferase("DETA.TXT")
EndIf
If File("MSG.TXT")
  	Ferase("MSG.TXT")
EndIf
If File("TRAI.TXT")
  	Ferase("TRAI.TXT")
EndIf

dbSelectArea("SA1")
dbSetOrder(1)

return

/*/


Ŀ
 Funcao     MODULO10()   Autor  Flavio Novaes     Data  03/02/2005 
Ĵ
 Descricao  Impressao de Boleto Bancario do Banco Itau com Codigo de   
            Barras, Linha Digitavel e Nosso Numero.                    
            Baseado no Fonte TBOL001 de 01/08/2002 de Raimundo Pereira.
Ĵ
 Uso        FINANCEIRO                                                 
ٱ


/*/
STATIC FUNCTION Modulo10(cData)
LOCAL L,D,P := 0
LOCAL B     := .F.
L := Len(cData)
B := .T.
D := 0
WHILE L > 0
	P := VAL(SUBSTR(cData, L, 1))
	IF (B)
		P := P * 2
		IF P > 9
			P := P - 9
		ENDIF
	ENDIF
	D := D + P
	L := L - 1
	B := !B
ENDDO
D := 10 - (Mod(D,10))
IF D = 10
	D := 0
ENDIF
RETURN(D)


/*


ͻ
Programa  NoAccent  Autor         Nava          Data   03/Nov/07  
͹
Desc.     Funcao para transformar texto acentuado em NAO acentuado    
                                                                      
͹
Uso       Generico                                                    
ͼ


*/

STATIC Function NoAccent( cText )

cText := STRTRAN( cText, CHR(198 ), "a" )
cText := STRTRAN( cText, CHR(228 ), "o" )
cText := STRTRAN( cText, CHR(160 ), "a" )
cText := STRTRAN( cText, CHR(130 ), "e" )
cText := STRTRAN( cText, CHR(161 ), "i" )
cText := STRTRAN( cText, CHR(162 ), "o" )
cText := STRTRAN( cText, CHR(163 ), "u" )
cText := STRTRAN( cText, CHR(133 ), "a" )
cText := STRTRAN( cText, CHR(138 ), "e" )
cText := STRTRAN( cText, CHR(141 ), "i" )
cText := STRTRAN( cText, CHR(149 ), "o" )
cText := STRTRAN( cText, CHR(151 ), "u" )
cText := STRTRAN( cText, CHR(131 ), "a" )
cText := STRTRAN( cText, CHR(136 ), "e" )
cText := STRTRAN( cText, CHR(140 ), "i" )
cText := STRTRAN( cText, CHR(147 ), "o" )
cText := STRTRAN( cText, CHR(150 ), "u" )
cText := STRTRAN( cText, CHR(199 ), "A" )
cText := STRTRAN( cText, CHR(229 ), "O" )
cText := STRTRAN( cText, CHR(181 ), "A" )
cText := STRTRAN( cText, CHR(144 ), "E" )
cText := STRTRAN( cText, CHR(214 ), "I" )
cText := STRTRAN( cText, CHR(224 ), "O" )
cText := STRTRAN( cText, CHR(233 ), "U" )
cText := STRTRAN( cText, CHR(183 ), "A" )
cText := STRTRAN( cText, CHR(212 ), "E" )
cText := STRTRAN( cText, CHR(222 ), "I" )
cText := STRTRAN( cText, CHR(227 ), "O" )
cText := STRTRAN( cText, CHR(235 ), "U" )
cText := STRTRAN( cText, CHR(182 ), "A" )
cText := STRTRAN( cText, CHR(210 ), "E" )
cText := STRTRAN( cText, CHR(215 ), "I" )
cText := STRTRAN( cText, CHR(226 ), "O" )
cText := STRTRAN( cText, CHR(128 ), "C" )
cText := STRTRAN( cText, CHR(135 ), "c" )

Return cText
